<!DOCTYPE html>
<html>
	<head>
		<title>Hyperbolic Fractal Explorer</title>
		<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
		<script   src="https://code.jquery.com/jquery-3.5.1.slim.min.js"   integrity="sha256-4+XzXVhsDmqanXGHaHvgh1gMQKX40OUvDEBTu8JcmNs="   crossorigin="anonymous"></script>
		<script id="shader-fs" type="x-shader/x-fragment">
		precision highp float;
		varying vec2 coord;
		uniform vec2 resolution;
		uniform vec2 xy;
		uniform vec2 zoomdir;
		uniform float zoom;

		float sinh(float a){
			return 0.5*(exp(a) - exp(-a));
		}

		float cosh(float a){
			return 0.5*(exp(a) + exp(-a));
		}        

		// Hyperbolic boost by eta
		vec3 hT(vec3 p, float eta){
			float x = cosh(eta)*p.x+sinh(eta)*p.z;
			float z = sinh(eta)*p.x+cosh(eta)*p.z;
			return vec3(x,p.y,z);
		}
		// Hyperbolic rotation by theta
		vec3 hR(vec3 p, float theta){
			float x = cos(theta)*p.x-sin(theta)*p.y;
			float y = sin(theta)*p.x+cos(theta)*p.y;
			return vec3(x,y,p.z);
		}
		// Null rotation by alpha
		vec3 hN(vec3 p, float a){
			float x = (1.0 - a*a*0.5)*p.x - a*p.y -a*a*0.5*p.z;
			float y = a*p.x + p.y - a*p.z;
			float z = -a*a*0.5*p.x - a*p.y + (1.0 + a*a*0.5)*p.z;
			return vec3(x,y,z);
		}
		// Lorentzian distance of p from the origin
		float dLsq(vec3 p){
			return p.x*p.x+p.y*p.y-(p.z-1.0)*(p.z-1.0);
		}
		// Calculate point after dilation of f
		vec3 hD(vec3 p, float f){
			float theta = atan(p.y,p.x);
			float phi = f*sqrt(dLsq(p));
			float x = cos(theta)*sinh(phi);
			float y = sin(theta)*sinh(phi);
			float z = cosh(phi);
			return vec3(x,y,z);
		}
		// Orbit function
		float mandelbrot( vec3 c ){
			float l = 0.0;
			vec3 o = vec3(0.,0.,1.); // Origin on the hyperboloid
			vec3 p = o; 
			float cd = sqrt(dLsq(c));
			float ctheta = atan(c.y,c.x);

			float s1 = 1800.0*cd*cd*cd*cd - 290.0*cd*cd - 205.0*cd + 260.0*c.x -3.0;
			float s2 = 16.0*(1.4*cd*cd+2.003*c.x+1.0) - 3.0;
			if( s1 < 0.0 ) return -2.;
			if( s2 < 0.0 ) return -2.;
			if( cd*cd > 4.0 ) return -2.;

			float ptheta = atan(p.y,p.x);
			float psq = dLsq(p);

			p = hT(o,cd);
			p = hR(p,ctheta);

			if(dLsq(p)>4.0) return -1.-log(log(log2(dLsq(p))));

			for ( int i = 0; i < 256; i ++) {
				ptheta = atan(p.y,p.x);
				psq = dLsq(p);

				p = hD(p,sqrt(psq));
				p = hR(p,ptheta-ctheta); 
				p = hT(p,cd);
				p = hR(p,ctheta);

				//p = hT(o,cd);
				//p = hR(p,ctheta);
				//p = hR(p,-2.*ptheta);
				//p = hT(p,psq);
				//p = hR(p,2.*ptheta);

				if(dLsq(p)>4.0) {break;}
				l += 1.0;
			}
			if (l>255.) return -2.0;
			return l-log(log(log2(dLsq(p))));
		}
		#define AA 1
		void main(void){
			vec3 col = vec3(0.0);
			for( int m=0; m<AA; m++ )
    		for( int n=0; n<AA; n++ )
    		{
				vec2 o = vec2(float(m),float(n))/float(AA)- 0.5;
				vec2 p = coord + o/resolution.x;
				float dp = dot(p,p);
				if(dot(p,p)<0.99){
					float x = 2.0*p.x/(1.0-dp);
					float y = 2.0*p.y/(1.0-dp);
					float z = (1.0+dp)/(1.0-dp);
					vec3 c = vec3(x,y,z);
					//if(abs(zoomdir.x)>0.001) c = hT(c,zoomdir.x);
					//if(abs(zoomdir.y)>0.001) c = hR(hT(hR(c,-2.0*atan(1.0)),zoomdir.y),2.0*atan(1.0));

					c = hD(c,pow(1.1-zoom,7.0));
					c = hT(c,xy.x);
					c = hR(hT(hR(c,-2.0*atan(1.0)),xy.y),2.0*atan(1.0));
					float l = mandelbrot(c);
					//col = vec3(mod(dLsq(c),2.));
					col += 0.5 + 0.5*cos( 3.0 + l*0.15 + vec3(0.0,0.6,1.0));
				} else {
					col += vec3(0.0,0.0,0.0);
				}
			}
			gl_FragColor = vec4(col/float(AA*AA), 1.0 );
		}
		</script>
		<script id="shader-vs" type="x-shader/x-vertex">
		attribute vec2 aPos;
		varying vec2 coord;
		void main(void) {
			coord = aPos.xy;
			gl_Position = vec4(aPos, 0., 1.);
		}
		</script>
	</head>
	<body style="overflow: hidden; margin: 0; text-align:center;background-color: #000">
		<canvas id="fractal"></canvas>
		<script src="dat.gui.js"></script>
		<script src="stats.min.js"></script>
		<script src="fractal.js?v=0.08"></script>
	</body>
</html>

